HAI 1.2

HOW IZ I POWERING YR BASE AN YR POWER
	BOTH SAEM POWER AN 0, O RLY?
		YA RLY, FOUND YR 1
	OIC

	I HAS A VALUE ITZ BASE
	IM IN YR PFUNC UPPIN YR I TIL BOTH SAEM I AN DIFF OF POWER AN 1
		VALUE R PRODUKT OF VALUE AN BASE
	IM OUTTA YR PFUNC

	FOUND YR VALUE
IF U SAY SO

O HAI IM LISTFACTORY
	HOW IZ I BUILDING
		I HAS A LIST ITZ LIEK A LISTFACTORY
		LIST HAS A LENGTH ITZ 0
		FOUND YR LIST
	IF U SAY SO

	HOW IZ I COPYING
		I HAS A LIST ITZ LIEK A LISTFACTORY
		LIST HAS A LENGTH ITZ 0
		
		IM IN YR LOOP UPPIN YR ITER TIL BOTH SAEM ITER AN ME'Z LENGTH
			LIST IZ PUSHING YR ME'Z SRS ITER MKAY
		IM OUTTA YR LOOP

		FOUND YR LIST
	IF U SAY SO

	HOW IZ I PUSHING YR ITEM
		ME HAS A SRS ME'Z LENGTH ITZ ITEM
		ME'Z LENGTH R SUM OF ME'Z LENGTH AN 1
	IF U SAY SO

	HOW IZ I GETTING YR INDEX
		DIFFRINT INDEX AN BIGGR OF INDEX AN ME'Z LENGTH, O RLY?
			YA RLY
				FOUND YR ME'Z SRS INDEX
		OIC		
	IF U SAY SO

	HOW IZ I PUTTING YR INDEX AN YR ITEM
		DIFFRINT INDEX AN BIGGR OF INDEX AN ME'Z LENGTH, O RLY?
			YA RLY
				ME'Z SRS INDEX R ITEM
		OIC
	IF U SAY SO
KTHX

I HAS A DIGITLENGTH, GIMMEH DIGITLENGTH, DIGITLENGTH IS NOW A NUMBR

HOW IZ I MAKINGYRDIGITS
	I HAS A DIGITS ITZ LISTFACTORY IZ BUILDING MKAY
	IM IN YR LOOP UPPIN YR ITER TIL BOTH SAEM ITER AN DIGITLENGTH
		DIGITS IZ PUSHING YR 0 MKAY
	IM OUTTA YR LOOP

	HOW IZ DIGITS PRINTING
		IM IN YR LOOP UPPIN YR ITER TIL BOTH SAEM ITER AN DIGITLENGTH
			VISIBLE ME IZ GETTING YR ITER MKAY!
		IM OUTTA YR LOOP
		VISIBLE ""
	IF U SAY SO

	HOW IZ DIGITS CONVERTING
		I HAS A TOTAL ITZ 0
		IM IN YR LOOP UPPIN YR ITER TIL BOTH SAEM ITER AN DIGITLENGTH
			I HAS A POWEROFTWO ITZ I IZ POWERING YR 2 AN YR DIFF OF DIFF OF DIGITLENGTH AN 1 AN ITER MKAY
			TOTAL R SUM OF TOTAL AN PRODUKT OF ME IZ GETTING YR ITER MKAY AN POWEROFTWO
		IM OUTTA YR LOOP
		
		FOUND YR TOTAL
	IF U SAY SO

	FOUND YR DIGITS
IF U SAY SO


I HAS A INPUT ITZ LISTFACTORY IZ BUILDING MKAY
I HAS A DIGIT ITZ 0
I HAS A DIGITLINE ITZ 0
IM IN YR LOOP
	I HAS A VALUE, GIMMEH VALUE
	BOTH SAEM VALUE AN "", O RLY?
		YA RLY, GTFO
	OIC
	VALUE IS NOW A NUMBR

	BOTH SAEM DIGIT AN 0, O RLY?
		YA RLY
			I HAS A NEWLINE ITZ I IZ MAKINGYRDIGITS MKAY
			INPUT IZ PUSHING YR NEWLINE MKAY
	OIC

	I HAS A LINE ITZ INPUT IZ GETTING YR DIGITLINE MKAY
	LINE IZ PUTTING YR DIGIT AN YR VALUE MKAY

	DIGIT R SUM OF DIGIT AN 1
	BOTH SAEM DIGIT AN DIGITLENGTH, O RLY?
		YA RLY
			DIGIT R 0
			DIGITLINE R SUM OF DIGITLINE AN 1
	OIC
IM OUTTA YR LOOP

HOW IZ I SUMMING YR LIST AN YR DIGIT
	I HAS A TOTAL ITZ 0
	IM IN YR LOOP UPPIN YR ITER TIL BOTH SAEM ITER AN LIST'Z LENGTH
		I HAS A LINE ITZ LIST IZ GETTING YR ITER MKAY
		I HAS A VALUE ITZ LINE IZ GETTING YR DIGIT MKAY
		TOTAL R SUM OF TOTAL AN VALUE
	IM OUTTA YR LOOP

	BOTH SAEM TOTAL AN BIGGR OF TOTAL AN DIFF OF LIST'Z LENGTH AN TOTAL 	BTW TOTAL >= LENGTH - TOTAL
	O RLY?
		YA RLY
			FOUND YR 1
		NO WAI
			FOUND YR 0
	OIC
IF U SAY SO

I HAS A OXYGENINPUT ITZ INPUT IZ COPYING MKAY
IM IN YR LOOP UPPIN YR ITER TIL BOTH SAEM ITER AN DIGITLENGTH
	I HAS A CORRECT ITZ I IZ SUMMING YR OXYGENINPUT AN YR ITER MKAY

	I HAS A NEWINPUT ITZ LISTFACTORY IZ BUILDING MKAY
	IM IN YR LOOP2 UPPIN YR ITER2 TIL BOTH SAEM ITER2 AN OXYGENINPUT'Z LENGTH
		I HAS A POTENTIALLINE ITZ OXYGENINPUT IZ GETTING YR ITER2 MKAY
		I HAS A POTENTIALVALUE ITZ POTENTIALLINE IZ GETTING YR ITER MKAY

		BOTH SAEM CORRECT AN POTENTIALVALUE, O RLY?
			YA RLY
				NEWINPUT IZ PUSHING YR POTENTIALLINE MKAY
		OIC
	IM OUTTA YR LOOP2

	OXYGENINPUT R NEWINPUT

	BOTH SAEM OXYGENINPUT'Z LENGTH AN 1, O RLY?
		YA RLY
			GTFO
	OIC
IM OUTTA YR LOOP

I HAS A COTWOINPUT ITZ INPUT IZ COPYING MKAY
IM IN YR LOOP UPPIN YR ITER TIL BOTH SAEM ITER AN DIGITLENGTH
	I HAS A CORRECT ITZ I IZ SUMMING YR COTWOINPUT AN YR ITER MKAY

	BOTH SAEM CORRECT AN 1, O RLY?
		YA RLY
			CORRECT R 0
		NO WAI
			CORRECT R 1
	OIC

	I HAS A NEWINPUT ITZ LISTFACTORY IZ BUILDING MKAY
	IM IN YR LOOP2 UPPIN YR ITER2 TIL BOTH SAEM ITER2 AN COTWOINPUT'Z LENGTH
		I HAS A POTENTIALLINE ITZ COTWOINPUT IZ GETTING YR ITER2 MKAY
		I HAS A POTENTIALVALUE ITZ POTENTIALLINE IZ GETTING YR ITER MKAY

		BOTH SAEM CORRECT AN POTENTIALVALUE, O RLY?
			YA RLY
				NEWINPUT IZ PUSHING YR POTENTIALLINE MKAY
		OIC
	IM OUTTA YR LOOP2

	COTWOINPUT R NEWINPUT

	BOTH SAEM COTWOINPUT'Z LENGTH AN 1, O RLY?
		YA RLY
			GTFO
	OIC
IM OUTTA YR LOOP

I HAS A RESULT ITZ OXYGENINPUT IZ GETTING YR 0 MKAY
RESULT IZ PRINTING MKAY
VISIBLE RESULT IZ CONVERTING MKAY

I HAS A RESULT2 ITZ COTWOINPUT IZ GETTING YR 0 MKAY
RESULT2 IZ PRINTING MKAY
VISIBLE RESULT2 IZ CONVERTING MKAY

VISIBLE PRODUKT OF RESULT IZ CONVERTING MKAY AN RESULT2 IZ CONVERTING MKAY

KTHXBYE