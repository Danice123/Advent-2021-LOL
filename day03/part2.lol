HAI 1.4

O HAI IM LISTFACTORY
	HOW IZ I BUILDING
		I HAS A LIST ITZ LIEK A LISTFACTORY
		LIST HAS A LENGTH ITZ 0
		FOUND YR LIST
	IF U SAY SO

	HOW IZ I PUSHING YR ITEM
		ME HAS A SRS ME'Z LENGTH ITZ ITEM
		ME'Z LENGTH R SUM OF ME'Z LENGTH AN 1
	IF U SAY SO

	HOW IZ I GETTING YR INDEX
		DIFFRINT INDEX AN BIGGR OF INDEX AN ME'Z LENGTH, O RLY?
			YA RLY
				FOUND YR ME'Z SRS INDEX
		OIC		
	IF U SAY SO

	HOW IZ I PUTTING YR INDEX AN YR ITEM
		DIFFRINT INDEX AN BIGGR OF INDEX AN ME'Z LENGTH, O RLY?
			YA RLY
				ME'Z SRS INDEX R ITEM
		OIC
	IF U SAY SO
KTHX

I HAS A DIGITLENGTH, GIMMEH DIGITLENGTH, DIGITLENGTH IS NOW A NUMBR

HOW IZ I MAKINGYRDIGITS
	I HAS A DIGITS ITZ LISTFACTORY IZ BUILDING MKAY
	IM IN YR LOOP UPPIN YR ITER TIL BOTH SAEM ITER AN DIGITLENGTH
		DIGITS IZ PUSHING YR 0 MKAY
	IM OUTTA YR LOOP

	HOW IZ DIGITS PRINTING
		IM IN YR LOOP UPPIN YR ITER TIL BOTH SAEM ITER AN DIGITLENGTH
			VISIBLE ME IZ GETTING YR ITER MKAY!
		IM OUTTA YR LOOP
		VISIBLE ""
	IF U SAY SO

	FOUND YR DIGITS
IF U SAY SO


I HAS A INPUT ITZ LISTFACTORY IZ BUILDING MKAY
I HAS A DIGIT ITZ 0
I HAS A DIGITLINE ITZ 0
IM IN YR LOOP
	I HAS A VALUE, GIMMEH VALUE
	BOTH SAEM VALUE AN "", O RLY?
		YA RLY, GTFO
	OIC
	VALUE IS NOW A NUMBR

	BOTH SAEM DIGIT AN 0, O RLY?
		YA RLY
			I HAS A NEWLINE ITZ I IZ MAKINGYRDIGITS MKAY
			INPUT IZ PUSHING YR NEWLINE MKAY
	OIC

	I HAS A LINE ITZ INPUT IZ GETTING YR DIGITLINE MKAY
	LINE IZ PUTTING YR DIGIT AN YR VALUE MKAY

	DIGIT R SUM OF DIGIT AN 1
	BOTH SAEM DIGIT AN DIGITLENGTH, O RLY?
		YA RLY
			DIGIT R 0
			DIGITLINE R SUM OF DIGITLINE AN 1
	OIC
IM OUTTA YR LOOP

HOW IZ I SUMMING YR DIGIT
	I HAS A TOTAL ITZ 0
	IM IN YR LOOP UPPIN YR ITER TIL BOTH SAEM ITER AN INPUT'Z LENGTH
		I HAS A LINE ITZ INPUT IZ GETTING YR ITER MKAY
		I HAS A VALUE ITZ LINE IZ GETTING YR DIGIT MKAY
		TOTAL R SUM OF TOTAL AN VALUE
	IM OUTTA YR LOOP

	VISIBLE TOTAL " >= " DIFF OF INPUT'Z LENGTH AN TOTAL
	BOTH SAEM TOTAL AN BIGGR OF TOTAL AN DIFF OF INPUT'Z LENGTH AN TOTAL 	BTW TOTAL >= LENGTH - TOTAL
	O RLY?
		YA RLY
			FOUND YR 1
		NO WAI
			FOUND YR 0
	OIC
IF U SAY SO

VISIBLE INPUT'Z LENGTH
IM IN YR LOOP UPPIN YR ITER TIL BOTH SAEM ITER AN DIGITLENGTH
	I HAS A CORRECT ITZ I IZ SUMMING YR ITER MKAY
	VISIBLE CORRECT

	I HAS A NEWINPUT ITZ LISTFACTORY IZ BUILDING MKAY
	IM IN YR LOOP2 UPPIN YR ITER2 TIL BOTH SAEM ITER2 AN INPUT'Z LENGTH
		I HAS A POTENTIALLINE ITZ INPUT IZ GETTING YR ITER2 MKAY
		POTENTIALLINE IZ PRINTING MKAY
		I HAS A POTENTIALVALUE ITZ POTENTIALLINE IZ GETTING YR ITER MKAY

		BOTH SAEM CORRECT AN POTENTIALVALUE, O RLY?
			YA RLY
				NEWINPUT IZ PUSHING YR POTENTIALLINE MKAY
		OIC
	IM OUTTA YR LOOP2

	VISIBLE "NEXT DIGIT"
	INPUT R NEWINPUT
IM OUTTA YR LOOP

I HAS A RESULT ITZ INPUT IZ GETTING YR 0 MKAY
RESULT IZ PRINTING MKAY

KTHXBYE