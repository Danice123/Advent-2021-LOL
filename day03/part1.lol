HAI 1.2

HOW IZ I POWERING YR BASE AN YR POWER
	BOTH SAEM POWER AN 0, O RLY?
		YA RLY, FOUND YR 1
	OIC

	I HAS A VALUE ITZ BASE
	IM IN YR PFUNC UPPIN YR I TIL BOTH SAEM I AN DIFF OF POWER AN 1
		VALUE R PRODUKT OF VALUE AN BASE
	IM OUTTA YR PFUNC

	FOUND YR VALUE
IF U SAY SO

O HAI IM LIST

	I HAS A LENGTH ITZ 0

	HOW IZ I PUSHING YR ITEM
		ME HAS A SRS ME'Z LENGTH ITZ ITEM
		ME'Z LENGTH R SUM OF ME'Z LENGTH AN 1
	IF U SAY SO

	HOW IZ I GETTING YR INDEX
		DIFFRINT INDEX AN BIGGR OF INDEX AN ME'Z LENGTH, O RLY?
			YA RLY
				FOUND YR ME'Z SRS INDEX
		OIC

		
	IF U SAY SO

	HOW IZ I PUTTING YR INDEX AN YR ITEM
		DIFFRINT INDEX AN BIGGR OF INDEX AN ME'Z LENGTH, O RLY?
			YA RLY
				ME'Z SRS INDEX R ITEM
		OIC
	IF U SAY SO
KTHX

I HAS A STRINGLENGTH, GIMMEH STRINGLENGTH, STRINGLENGTH IS NOW A NUMBR
I HAS A SUMS ITZ LIEK A LIST
IM IN YR BUILDSUMS UPPIN YR ITER TIL BOTH SAEM ITER AN STRINGLENGTH
	SUMS IZ PUSHING YR 0 MKAY
IM OUTTA YR BUILDSUMS

I HAS A LENGTH ITZ 0
I HAS A DIGIT ITZ 0
IM IN YR SUMPROCESS
    I HAS A VALUE, GIMMEH VALUE
    BOTH SAEM VALUE AN "", O RLY?
        YA RLY, GTFO
    OIC
	VALUE IS NOW A NUMBR

	SUMS IZ PUTTING YR DIGIT AN YR SUM OF VALUE AN SUMS IZ GETTING YR DIGIT MKAY MKAY

	DIGIT R SUM OF DIGIT AN 1
	BOTH SAEM DIGIT AN STRINGLENGTH, O RLY?
		YA RLY
			DIGIT R 0
			LENGTH R SUM OF LENGTH AN 1
	OIC
IM OUTTA YR SUMPROCESS

I HAS A GAMMAVALUE ITZ 0
I HAS A EPSILONVALUE ITZ 0
I HAS A GAMMA ITZ A YARN
I HAS A EPSILON ITZ A YARN
IM IN YR MAKEBINARY UPPIN YR SUMITER TIL BOTH SAEM SUMITER AN STRINGLENGTH
	I HAS A VAL ITZ SUMS IZ GETTING YR DIFF OF DIFF OF STRINGLENGTH AN SUMITER AN 1 MKAY
	I HAS A ADD ITZ I IZ POWERING YR 2 AN YR SUMITER MKAY
	DIFFRINT VAL AN SMALLR OF VAL AN QUOSHUNT OF LENGTH AN 2, O RLY?
		YA RLY
			GAMMAVALUE R SUM OF GAMMAVALUE AN ADD
			GAMMA R SMOOSH 1 GAMMA MKAY
			EPSILON R SMOOSH 0 EPSILON MKAY
		NO WAI
			EPSILONVALUE R SUM OF EPSILONVALUE AN ADD
			GAMMA R SMOOSH 0 GAMMA MKAY
			EPSILON R SMOOSH 1 EPSILON MKAY
	OIC
IM OUTTA YR MAKEBINARY

VISIBLE GAMMA
VISIBLE GAMMAVALUE
VISIBLE EPSILON
VISIBLE EPSILONVALUE

VISIBLE PRODUKT OF GAMMAVALUE AN EPSILONVALUE

KTHXBYE